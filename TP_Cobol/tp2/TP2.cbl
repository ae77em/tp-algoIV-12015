       IDENTIFICATION DIVISION.
       PROGRAM-ID. TP2.
       AUTHOR. Grupo-NO-C-Q-Nro.

       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
            DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.

         SELECT ARCH-PARAMETROS ASSIGN TO DISK
             ORGANIZATION IS LINE SEQUENTIAL
             FILE STATUS IS FS-PARAMETROS.

         SELECT ARCH-TRABAJOS-X-EMPRESA ASSIGN TO DISK
             ORGANIZATION IS LINE SEQUENTIAL
             FILE STATUS IS FS-T-X-E.

         SELECT ARCH-TAR-IDX ASSIGN TO DISK
             ORGANIZATION IS INDEXED
             ACCESS MODE IS DYNAMIC
             RECORD KEY IS REG-TAR-IND
             FILE STATUS IS FS-ARCH-TAR-IDX.

         SELECT ARCH-EMP-IDX ASSIGN TO DISK
             ORGANIZATION IS INDEXED
             ACCESS MODE IS RANDOM
             RECORD KEY IS REG-EMP-IND-COD
             ALTERNATE RECORD KEY IS REG-EMP-IND-CUIT
             FILE STATUS IS FS-ARCH-EMP-IDX.

         SELECT ARCH-CONS-INDEXED ASSIGN TO DISK
             ORGANIZATION IS INDEXED
             ACCESS MODE IS RANDOM
             RECORD KEY IS REG-KEY-CODE-CONS
             FILE STATUS IS FS-ARCH-CONS-INDEXED.

         SELECT ARCH-TIMES-IDX ASSIGN TO DISK
             ORGANIZATION IS INDEXED
             ACCESS MODE IS DYNAMIC
             RECORD KEY IS REG-KEY-TIMES
             ALTERNATE RECORD KEY IS REG-KEY-CUIT WITH DUPLICATES
             FILE STATUS IS FS-ARCH-TIMES-IDX.

       DATA DIVISION. FILE SECTION.

       FD ARCH-PARAMETROS LABEL RECORD IS STANDARD
               VALUE OF FILE-ID IS 'PARAMETROS.DAT'
               DATA RECORD IS REG-PARAMETROS.

       01 REG-PARAMETROS               PIC 9(11).

       FD ARCH-TIMES-IDX LABEL RECORD IS STANDARD
               VALUE OF FILE-ID IS 'TIMES-INDEXED.DAT'
               DATA RECORD IS REG-TIMES-INDEXED.

       01 REG-TIMES-INDEXED.
         03 REG-KEY-TIMES.
           05  REG-KEY-NUM            PIC X(5).
           05  REG-KEY-FECHA          PIC 9(8).
           05  REG-KEY-CUIT           PIC 9(11).
         03 REG-TIMES-TAREA             PIC X(4).
         03 REG-TIMES-HORAS             PIC 9(2)v99.

       FD ARCH-EMP-IDX LABEL RECORD IS STANDARD
            VALUE OF FILE-ID IS 'EMPRESAS-INDEXED.DAT'
            DATA RECORD IS REG-EMP-INDEXED.

       01 REG-EMP-INDEXED.
          03 REG-EMP-IND-COD          PIC 9(3).
          03 REG-EMP-IND-RAZON        PIC X(25).
          03 REG-EMP-IND-DIRECCION     PIC X(20).
          03 REG-EMP-IND-TEL          PIC X(20).
          03 REG-EMP-IND-CUIT         PIC 9(11).

       FD ARCH-CONS-INDEXED LABEL RECORD IS STANDARD
         VALUE OF FILE-ID IS 'CONSULTORES-INDEXED.DAT'
         DATA RECORD IS REG-CONSULTORES-INDEXED.

       01 REG-CONSULTORES-INDEXED.
         03 REG-KEY-CODE-CONS      PIC X(5).
         03 REG-CONS-DNI          PIC 9(8).
         03 REG-CONS-SRT          PIC X(2).
         03 REG-CONS-NOMBRE       PIC X(25).
         03 REG-CONS-DIRECCION     PIC X(20).
         03 REG-CONS-TELEFONO      PIC X(20).

       FD ARCH-TAR-IDX LABEL RECORD IS STANDARD
         VALUE OF FILE-ID IS 'TARIFAS-INDEXED.DAT'
         DATA RECORD IS REG-TAR-INDEXED.

       01 REG-TAR-INDEXED.
          03 REG-TAR-IND.
              05 REG-TAR-SRT-IND       PIC X(2).
             05 REG-TAR-VIG-DESDE-IND     PIC 9(8).
          03 REG-TAR-TARIFA-IND        PIC 9(5)V99.

       SD ARCH-TRABAJOS-X-EMPRESA
         DATA RECORD IS REG-T-X-E.

       01 REG-T-X-E.
         03 REG-T-X-E-RAZON             PIC X(25).
         03 REG-T-X-E-CUIT              PIC 9(11).
         03 REG-T-X-E-FECHA             PIC 9(8).
         03 REG-T-X-E-COD-CONS          PIC X(5).
         03 REG-T-X-E-HORAS             PIC 9(2)V99.

       WORKING-STORAGE SECTION.

       77 FS-PARAMETROS PIC XX.
         88 PARAMETROS-OK   VALUE '00'.
         88 PARAMETROS-NOENC  VALUE '23'.
         88 PARAMETROS-EOF  VALUE '10'.

       77 FS-T-X-E PIC XX.
         88 T-X-E-OK   VALUE '00'.
         88 T-X-E-NOENC  VALUE '23'.
         88 T-X-E-EOF  VALUE '10'.

       77 FS-ARCH-TIMES-IDX PIC XX.
         88 TIMES-OK   VALUE '00'.
         88 TIMES-NOENC  VALUE '23'.
         88 TIMES-EOF  VALUE '10'.


       77 FS-ARCH-EMP-IDX  PIC XX.
         88 EMP-OK   VALUE '00'.
         88 EMP-NOENC  VALUE '23'.
         88 EMP-EOF  VALUE '10'.

       77 FS-ARCH-TAR-IDX  PIC XX.
         88 TAR-OK   VALUE '00'.
         88 TAR-NOENC  VALUE '23'.
         88 TAR-EOF  VALUE '10'.

       77 FS-ARCH-CONS-INDEXED PIC XX.
         88 CONS-OK   VALUE '00'.
         88 CONS-NOENC  VALUE '23'.
         88 CONS-EOF  VALUE '10'.

       01  WS-LETRA       PIC X.
       01  WS-CANT-REG     PIC 9(3).

       01  WS-MIN-CUIT     PIC 9(11).
       01  WS-MAX-CUIT     PIC 9(11).

       01 WS-COD-OPER                 PIC X.
       01 WS-CUIT                     PIC 9(11).
       01 WS-RAZON                    PIC X(25).
       01 WS-COD-ERROR                PIC XX.
       01 ACUM     PIC 999 VALUE 000.

       01 WS-CORTE-FECHA              PIC X(8).
       01 WS-CORTE-RAZON-SOCIAL       PIC X(25).

       01 REP-ENC-GRAL.
         03 RE-FECHA.
            05 FECHA-DD     PIC 99.
            05 BARRA1       PIC X VALUE '/'.
            05 FECHA-MM     PIC 99.
            05 BARRA2       PIC X VALUE '/'.
            05 FECHA-AAAA   PIC 9999.
         03 RE-ESPACIOS    PIC X(62) VALUE SPACES.
         03 RE-NRO-PAGINA  PIC X(5) VALUE 'Nro: '.
         03 RE-PAGINA      PIC 999.

       01 REP-ENC-FECHA.
         03 REF-FECHA  PIC X(15) VALUE 'Fecha'.
         03 REF-CONS  PIC X(25) VALUE 'Consultor'.
         03 REF-NOMB  PIC X(22) VALUE 'Nombre   '.
         03 REF-HORAS  PIC X(10) VALUE 'Horas   '.
         03 REF-IMPOR  PIC X(10) VALUE 'Importe  '.

       01 REP-LINEA-FECHA.
         03 RLF-FECHA.
           05 RLF-DD     PIC 99.
           05 RLF-BARRA1 PIC X VALUE '/'.
           05 RLF-MM     PIC 99.
           05 RLF-BARRA2 PIC X VALUE '/'.
           05 RLF-AAAA   PIC 9999.
           05 RLF-AUX    PIC X(5) VALUE SPACES.
         03 RLF-CONS  PIC X(25).
         03 RLF-NOMB  PIC X(20).
         03 RLF-HORAS  PIC 9(8).
         03 RLF-AUX    PIC X(2) VALUE SPACES.
         03 RLSF-IMPOR  PIC 9(10).

       01 REP-TOT-FECHA.
         03 RTF-FECHA  PIC X(20) VALUE 'Totales por fecha:'.
         03 RTF-AUX  PIC X(40) VALUE SPACES.
         03 RTF-TOTAL-HORAS  PIC 9(8).
         03 RTF-AUX  PIC X(2) VALUE SPACES.
         03 RTF-TOTAL-IMPOR  PIC 9(10).

       01 REP-TOT-GRAL.
         03 RTG-FECHA  PIC X(20) VALUE 'Total general:'.
         03 RTG-AUX  PIC X(48) VALUE SPACES.
         03 RTG-TOTAL-GRAL  PIC 9(10)V99.

       01 LIN-TOT-FECHA.
         03 LINS-AUX  PIC X(60) VALUE SPACES.
         03 LIN-TOTAL-HORAS  PIC X(10) VALUE ALL "-".
         03 LINS-TOTAL-IMPOR  PIC X(10) VALUE ALL "-".

       01 LINEA-DIVISORIA-CONTINUA.
         03 FILLER PIC X(80) VALUE ALL "_".

       01 FECHA.
         03 AAAA   PIC 9(4).
         03 MM     PIC 9(2).
         03 DD     PIC 9(2).

       01 PAGINA   PIC 999.
       01 NRO-LINEA PIC 99.
       01 IMPORTE PIC 9(10)V99.
       01 IMPORTE-FECHA PIC 9(10)V99.
       01 IMPORTE-TOTAL PIC 9(10)V99.


       PROCEDURE DIVISION.
       TP2.

         PERFORM INICIO.

          SORT ARCH-TRABAJOS-X-EMPRESA ON ASCENDING KEY
                         REG-T-X-E-RAZON,
                         REG-T-X-E-CUIT,
                         REG-T-X-E-FECHA,
                         REG-T-X-E-COD-CONS
                INPUT PROCEDURE IS CARGAR-ARCHIVO-SORT
                OUTPUT PROCEDURE IS PROCESAR-ARCHIVO-SORT.

          STOP RUN.

       INICIO.
          MOVE FUNCTION CURRENT-DATE TO FECHA.

          MOVE 0 TO PAGINA.
          MOVE 0 TO NRO-LINEA.

          MOVE DD TO FECHA-DD.
          MOVE MM TO FECHA-MM.
          MOVE AAAA TO FECHA-AAAA.

       IMPRIMIR-ENC-PAGINA.

          MOVE 1 TO NRO-LINEA.
          ADD 1 TO PAGINA.
          MOVE PAGINA TO RE-PAGINA.
          DISPLAY REP-ENC-GRAL.
          DISPLAY '                        Horas Aplicadas por Empresa'.
          DISPLAY ' '.
          DISPLAY "Empresa: " REG-T-X-E-RAZON .
          DISPLAY "CUIT: " REG-T-X-E-CUIT.
          ADD 5 TO NRO-LINEA.

       CARGAR-ARCHIVO-SORT.
         MOVE 0 TO ACUM.
         PERFORM ABRIR-ARCHIVOS.
         PERFORM LEER-Y-SETEAR-PARAMETROS.
         PERFORM POSICIONAR-MAESTRO-TIMES.
         PERFORM LEER-MAESTRO-TIMES.
         PERFORM CARGAR-ARCHIVO
         UNTIL REG-KEY-CUIT IS GREATER THAN WS-MAX-CUIT OR TIMES-EOF.

         PERFORM CERRAR-ARCHIVOS.

       ABRIR-ARCHIVOS.
         OPEN INPUT ARCH-TIMES-IDX.
         OPEN INPUT ARCH-PARAMETROS.

         MOVE 'A' TO WS-COD-OPER.

         CALL 'EMPRESAS'
           USING WS-COD-OPER, WS-CUIT, WS-RAZON, WS-COD-ERROR.

       LEER-Y-SETEAR-PARAMETROS.
         READ ARCH-PARAMETROS.
         MOVE REG-PARAMETROS TO WS-MIN-CUIT.

         READ ARCH-PARAMETROS.
         MOVE REG-PARAMETROS TO WS-MAX-CUIT.

       POSICIONAR-MAESTRO-TIMES.
         MOVE WS-MIN-CUIT TO REG-KEY-CUIT.
         START ARCH-TIMES-IDX KEY IS NOT LESS REG-KEY-CUIT.

       LEER-MAESTRO-TIMES.
         READ ARCH-TIMES-IDX NEXT RECORD.

       CARGAR-ARCHIVO.
         ADD 1 TO ACUM.
         PERFORM BUSCAR-RAZON-SOCIAL.
         MOVE WS-CUIT          TO REG-T-X-E-CUIT.
         MOVE WS-RAZON         TO REG-T-X-E-RAZON.
         MOVE REG-KEY-FECHA    TO REG-T-X-E-FECHA.
         MOVE REG-KEY-NUM      TO REG-T-X-E-COD-CONS.
         MOVE REG-TIMES-HORAS  TO REG-T-X-E-HORAS.

         RELEASE REG-T-X-E.

         PERFORM LEER-MAESTRO-TIMES.

       BUSCAR-RAZON-SOCIAL.
         MOVE REG-KEY-CUIT TO WS-CUIT.
         MOVE 'L' TO WS-COD-OPER.
         CALL 'EMPRESAS'
                   USING WS-COD-OPER, WS-CUIT, WS-RAZON, WS-COD-ERROR.


       CERRAR-ARCHIVOS.
         CLOSE ARCH-TIMES-IDX.
         CLOSE ARCH-PARAMETROS.

         MOVE 'C' TO WS-COD-OPER.

         CALL 'EMPRESAS'
                 USING WS-COD-OPER, WS-CUIT, WS-RAZON, WS-COD-ERROR.
      ****************************************
       PROCESAR-ARCHIVO-SORT.
          MOVE 0 TO WS-CANT-REG.

          PERFORM ABRIR-ARCHIVOS-LUEGO-SORT.
          PERFORM LEER-ARCH-TRABAJOS-X-EMPRESA.
          PERFORM PROCESAR-LISTADO UNTIL T-X-E-EOF.
          PERFORM CERRAR-ARCHIVOS-LUEGO-SORT.


       ABRIR-ARCHIVOS-LUEGO-SORT.
         OPEN INPUT ARCH-CONS-INDEXED.
         OPEN INPUT ARCH-TAR-IDX.

       LEER-ARCH-TRABAJOS-X-EMPRESA.
          RETURN ARCH-TRABAJOS-X-EMPRESA AT END SET T-X-E-EOF TO TRUE
          END-RETURN.

       CERRAR-ARCHIVOS-LUEGO-SORT.
         CLOSE ARCH-CONS-INDEXED.
         CLOSE ARCH-TAR-IDX.

       PROCESAR-LISTADO.
         PERFORM ASIGNAR-CORTE-RAZON-SOCIAL.
         PERFORM IMPRIMIR-ENC-PAGINA
         PERFORM PROCESAR-TRABAJOS-X-RAZON UNTIL
                     WS-CORTE-RAZON-SOCIAL NOT EQUAL REG-T-X-E-RAZON
                      OR T-X-E-EOF.

         MOVE IMPORTE-TOTAL TO RTG-TOTAL-GRAL.
         DISPLAY IMPORTE-TOTAL.
         DISPLAY ' '.
         DISPLAY REP-TOT-GRAL.

       ASIGNAR-CORTE-RAZON-SOCIAL.
          MOVE REG-T-X-E-RAZON TO WS-CORTE-RAZON-SOCIAL.

       PROCESAR-TRABAJOS-X-RAZON.
           PERFORM ASIGNAR-CORTE-FECHA.

           DISPLAY ' '.
           DISPLAY REP-ENC-FECHA.
           DISPLAY LINEA-DIVISORIA-CONTINUA.
           DISPLAY ' '.
           ADD 4 TO NRO-LINEA.

           PERFORM PROCESAR-TRABAJOS-X-FECHA UNTIL
               WS-CORTE-RAZON-SOCIAL NOT EQUAL REG-T-X-E-RAZON
               OR WS-CORTE-FECHA NOT EQUAL REG-T-X-E-FECHA
               OR T-X-E-EOF.

           DISPLAY LIN-TOT-FECHA.
           DISPLAY REP-TOT-FECHA.
           ADD 2 TO NRO-LINEA.
           MOVE 0 TO IMPORTE.
           MOVE 0 TO RTF-TOTAL-HORAS.

           IF NRO-LINEA IS GREATER OR EQUALS 60
             PERFORM IMPRIMIR-ENC-PAGINA
             MOVE 0 TO NRO-LINEA.

       ASIGNAR-CORTE-FECHA.
          MOVE REG-T-X-E-FECHA TO WS-CORTE-FECHA.

       PROCESAR-TRABAJOS-X-FECHA.

         PERFORM OBTENER-DATOS-CONSULTOR.
         PERFORM CALCULAR-IMPORTE.
         PERFORM CARGAR-LINEA-REPORTE.

         ADD REG-T-X-E-HORAS TO RTF-TOTAL-HORAS.
         ADD IMPORTE TO RTF-TOTAL-IMPOR.
         ADD IMPORTE TO IMPORTE-FECHA.
         ADD IMPORTE TO IMPORTE-TOTAL.

         DISPLAY REP-LINEA-FECHA.

         ADD 1 TO NRO-LINEA.

         PERFORM LEER-ARCH-TRABAJOS-X-EMPRESA.

       OBTENER-DATOS-CONSULTOR.
        MOVE REG-T-X-E-COD-CONS TO REG-KEY-CODE-CONS.
        READ ARCH-CONS-INDEXED RECORD KEY IS REG-KEY-CODE-CONS.

       CALCULAR-IMPORTE.

         MOVE REG-CONS-SRT TO REG-TAR-SRT-IND.
         MOVE REG-T-X-E-FECHA TO REG-TAR-VIG-DESDE-IND.

         START ARCH-TAR-IDX
           KEY IS NOT GREATER OR EQUAL TO REG-TAR-IND.

         READ ARCH-TAR-IDX NEXT RECORD.

         MOVE REG-TAR-TARIFA-IND TO IMPORTE.
         MULTIPLY REG-T-X-E-HORAS BY IMPORTE.

       CARGAR-LINEA-REPORTE.
         MOVE IMPORTE TO RLSF-IMPOR.
         MOVE REG-T-X-E-FECHA(1:4) TO RLF-AAAA.
         MOVE REG-T-X-E-FECHA(5:2) TO RLF-MM.
         MOVE REG-T-X-E-FECHA(7:2) TO RLF-DD.
         MOVE REG-T-X-E-COD-CONS TO RLF-CONS.
         MOVE REG-T-X-E-HORAS TO RLF-HORAS.
         ADD 1 TO NRO-LINEA.
